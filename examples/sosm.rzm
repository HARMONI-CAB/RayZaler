#
# Copyright (c) 2023 Gonzalo J. Carracedo <BatchDrake@gmail.com>
#
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are met:
#
# 1. Redistributions of source code must retain the above copyright notice, this
#    list of conditions and the following disclaimer.
#
# 2. Redistributions in binary form must reproduce the above copyright notice,
#    this list of conditions and the following disclaimer in the documentation
#    and/or other materials provided with the distribution.
#
# 3. Neither the name of the copyright holder nor the names of its contributors
#    may be used to endorse or promote products derived from this software
#    without specific prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
# AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
# IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
# DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE
# FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
# DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
# SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
# CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY,
# OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
# OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
#

################################################################################
#
# SmarActGoniometer: implemented according to SmarAct's CGO-60-5 goniometer
# stage, as per https://www.smaract.com/en/goniometer-stages/product/cgo-60-5-goniometer-stage
#
# Degrees of freedom:
#   alpha: angle of rotation of the positioning stage [-5, +5]
#
# Ports:
#   platform: top platform on which additional modules are plugged
#
#

element SmarActGoniometer {
  dof alpha = 0;

  translate origin (dz = 4.25e-3)
    BlockElement base(length = 50e-3, width  = 50e-3, height = 8.5e-3);

  on top_side of origin.base {
    translate (dz = 69e-3) {  # 60.5 mm + 8.5 mm
      rotate (alpha, 1, 0, 0) {
        translate (dz = -60.5e-3) {
          port platform; # Platform is 60.5 mm below the axis of rotation
          translate (dz = -4.25e-3)
            BlockElement bed(length = 50e-3, width  = 50e-3, height = 8.5e-3);
        }
      }
    }
  }
}

################################################################################
#
# SOSM: SCAOS Object Selection Mirror for HARMONI
#
# Degrees of freedom:
#   x: Angle in the X stage
#   y: Angle in the Y stage
#
# Optical: yes
#

element SOSM {
  dof x = 0;
  dof y = 0;

  SmarActGoniometer gonX(alpha = x);

  on platform of gonX {
    rotate (90, 0, 0, 1) {
      SmarActGoniometer gonY(alpha = y);
      on platform of gonY
        FlatMirror mirror(radius = 20e-3, thickness = 5e-3);
    }
  }

  path mirror;
}

#
# Initialize SOSM with a mobile 
#
SOSM osm(x = 5 * cos(0.1 * t), y = 5 * cos(0.15 * t));

path osm;
